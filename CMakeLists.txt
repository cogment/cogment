cmake_minimum_required(VERSION 3.10)

# Enables passing <Library>_ROOT as the directory to find <Library>-config.cmake files
cmake_policy(SET CMP0074 NEW)

if(${CMAKE_SYSTEM_NAME} MATCHES "Darwin" AND NOT DEFINED CMAKE_OSX_DEPLOYMENT_TARGET)
  message(WARNING "CMAKE_OSX_DEPLOYMENT_TARGET is not defined, set it to the earliset version of macos you want to target")
endif()

project(cogment_orchestrator)

set(Protobuf_USE_STATIC_LIBS ON)
# "NO_MODULE" makes sure the "protobuf-config.cmake" file that's generated by the source build is used instead of CMake built-in FindProtobuf.cmake
find_package(Protobuf REQUIRED NO_MODULE)
find_package(gRPC REQUIRED)
find_package(prometheus-cpp REQUIRED)
find_package(spdlog REQUIRED)

add_subdirectory(third_party/slt_concur)
add_subdirectory(third_party/slt_setting)

find_program(CLANG_TIDY "clang-tidy")

if (APPLE)
  find_library(UUID_LIB CoreFoundation )
else()
  set(UUID_LIB uuid.a)
endif()

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

add_subdirectory(lib)

SET(ORCHESTRATOR_SRC
  main.cpp
)

add_executable(orchestrator ${ORCHESTRATOR_SRC})
IF(CLANG_TIDY)
  set_target_properties(orchestrator PROPERTIES CXX_CLANG_TIDY "clang-tidy;--format-style=file")
ENDIF()

target_include_directories(orchestrator PRIVATE .)

target_link_libraries(orchestrator
    debug -rdynamic
    orchestrator_lib
    prometheus-cpp::pull
    gRPC::grpc
)

set_target_properties(orchestrator PROPERTIES DEBUG_POSTFIX _debug)

install(TARGETS orchestrator
        DESTINATION bin)

############################ code format ############################
if(NOT DEFINED CLANG_FORMAT_BIN)
  find_program(CLANG_FORMAT_BIN NAMES clang-format)
endif()

file(GLOB_RECURSE ALL_SOURCE_FILES *.cpp *.h *.cxx *.hxx *.hpp *.cc *.ipp)

set(CLANG_FORMAT_EXCLUDE_PATTERNS ${CLANG_FORMAT_EXCLUDE_PATTERNS}
  "/CMakeFiles/"
  "cmake"
  "third_party"
)

foreach (SOURCE_FILE ${ALL_SOURCE_FILES})
    foreach (EXCLUDE_PATTERN ${CLANG_FORMAT_EXCLUDE_PATTERNS})
        string(FIND ${SOURCE_FILE} ${EXCLUDE_PATTERN} EXCLUDE_FOUND)
        if (NOT ${EXCLUDE_FOUND} EQUAL -1)
            list(REMOVE_ITEM ALL_SOURCE_FILES ${SOURCE_FILE})
        endif ()
    endforeach ()
endforeach ()


add_custom_target(format
    COMMENT "Running clang-format to change files"
    COMMAND ${CLANG_FORMAT_BIN}
    -style=file
    -i
    ${ALL_SOURCE_FILES}
)
#############################################################

